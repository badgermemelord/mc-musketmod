plugins {
    id 'fabric-loom' version '0.12-SNAPSHOT'
}

file("../gradle.properties").withInputStream {
    def props = new Properties()
    props.load(it)
    props.each { prop -> project.ext.set(prop.key, prop.value) }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = mod_version
group = maven_group
archivesBaseName = "${mod_name}-${minecraft_version}-fabric"

eclipse {
    project { name = "${mod_name}-${minecraft_version}-fabric" }
}

sourceSets.main.java { srcDir '../src/main/java' }
sourceSets.main.resources { srcDir '../src/main/resources' }

loom {
    runs {
        client {
            runDir = '../run'
            programArgs.add('--username')
            programArgs.add('Dev')
        }
        server {
            runDir = '../run'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation("org.joml:joml:1.10.4") { transitive = false }
    implementation("org.joml:joml-primitives:1.10.0") { transitive = false }
    modApi("org.valkyrienskies:valkyrienskies-118-common:${rootProject.vs2_version}")
    compileOnly("org.valkyrienskies.core:api:${rootProject.vs_core_version}")
    compileOnly("org.valkyrienskies.core:api-game:${rootProject.vs_core_version}")
    compileOnly("org.valkyrienskies.core:util:${rootProject.vs_core_version}")
    compileOnly("org.valkyrienskies.core:impl:${rootProject.vs_core_version}")
    modApi("org.valkyrienskies:valkyrienskies-118-fabric:${rootProject.vs2_version}")
}

repositories {
    maven {
        name("Valkyrien Skies Internal")
        url("https://maven.valkyrienskies.org")
    }
}


processResources {
    inputs.property "version", mod_version

    filesMatching("fabric.mod.json") {
        expand "version": mod_version
    }
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}
